Avalanche Noise Amplifier and Bit Generator
* documentation and schematics at http://holdenc.altervista.org/avalanche/

Vcc 20 0 dc 13V

* We model the avalanche noise produced by a reverse-biased
* BE junction with a pseudo-random noise generator inside spice
R1 20 1 150k
Vn 1 n1 dc 0V ac 1mV trrandom (1 5us 0s 125m 0m)
Vbk n1 2 dc 10.8V
Q2 1 2 0 Q2n3904

* Buffer
C1 1 3 470n
R2 16 3 1Meg
x1 3 4 20 0 4 tl082

* Inverting Amplifier
C2 4 5 470n
R3 16 6 1Meg
R4 5 7 12k
R5 7 8 47k
x2 6 7 20 0 8 tl082

* Constant Current Generator
R6 20 9 1.2k
xd1 20 10 1N4148
xd2 10 11 1N4148
R7 11 0 10k
Q3 12 11 9 Q2n3906

* Differential Pair
Q4 13 8 12 Q2n3906
Q5 14 16 12 Q2n3906
R8 13 0 10k
R9 14 0 10k

* Virtual ground
R10 20 15 100k
R11 15 0 100k
C3 15 0 4.7u
x3 15 16 20 0 16 tl082

.op
.tran 0.1m 5m 0m 1u
.ac dec 1000 1 10Meg

.control
set color0=white
set color1=black
set hcopypscolor=1
set hcopywidth=1600
set hcopyheight=600
run
*show
setplot op1
print all

setplot ac1
plot mag(v(8)/v(1))
hardcopy frequency_response.eps mag(v(8)/v(1)) xlabel frequency

setplot tran1
plot v(8) v(16) v(13)
hardcopy noise.eps v(8) v(16) v(13) xlabel time
.endc

* model from http://www.onsemi.com/PowerSolutions/product.do?id=2N3904
**************************************
*      Model Generated by MODPEX     *
*Copyright(c) Symmetry Design Systems*
*         All Rights Reserved        *
*    UNPUBLISHED LICENSED SOFTWARE   *
*   Contains Proprietary Information *
*      Which is The Property of      *
*     SYMMETRY OR ITS LICENSORS      *
*Commercial Use or Resale Restricted *
*   by Symmetry License Agreement    *
**************************************
* Model generated on Aug 7, 01
* MODEL FORMAT: PSpice
*$
.MODEL Q2n3904 npn (
+IS=1.26532e-10 BF=206.302 NF=1.5 VAF=1000
+IKF=0.0272221 ISE=2.30771e-09 NE=3.31052 BR=20.6302
+NR=2.89609 VAR=9.39809 IKR=0.272221 ISC=2.30771e-09
+NC=1.9876 RB=5.8376 IRB=50.3624 RBM=0.634251
+RE=0.0001 RC=2.65711 XTB=0.1 XTI=1
+EG=1.05 CJE=4.64214e-12 VJE=0.4 MJE=0.256227
+TF=4.19578e-10 XTF=0.906167 VTF=8.75418 ITF=0.0105823
+CJC=3.76961e-12 VJC=0.4 MJC=0.238109 XCJC=0.8
+FC=0.512134 CJS=0 VJS=0.75 MJS=0.5
+TR=6.82023e-08 PTF=0 KF=0 AF=1)

* model from http://www.onsemi.com/PowerSolutions/product.do?id=2N3906
**************************************
*      Model Generated by MODPEX     *
*Copyright(c) Symmetry Design Systems*
*         All Rights Reserved        *
*    UNPUBLISHED LICENSED SOFTWARE   *
*   Contains Proprietary Information *
*      Which is The Property of      *
*     SYMMETRY OR ITS LICENSORS      *
*Commercial Use or Resale Restricted *
*   by Symmetry License Agreement    *
**************************************
* Model generated on Aug 7, 01
* MODEL FORMAT: PSpice
.MODEL q2n3906 pnp
+IS=7.75521e-12 BF=194.093 NF=1.35509 VAF=156.436
+IKF=0.0660057 ISE=1.88546e-12 NE=1.81673 BR=3.41317
+NR=1.5 VAR=5.86061 IKR=1.70599 ISC=7.64281e-10
+NC=1.92376 RB=6.48961 IRB=0.1 RBM=0.1
+RE=0.0001 RC=2.45044 XTB=0.1 XTI=1
+EG=1.05 CJE=6.11928e-12 VJE=0.4 MJE=0.248812
+TF=5.21843e-10 XTF=0.932702 VTF=9.1046 ITF=0.0106472
+CJC=6.85007e-12 VJC=0.4 MJC=0.279018 XCJC=0.9
+FC=0.478887 CJS=0 VJS=0.75 MJS=0.5
+TR=4.30605e-07 PTF=0 KF=0 AF=1

* model from http://focus.ti.com/docs/prod/folders/print/tl082.html
* TL082 OPERATIONAL AMPLIFIER "MACROMODEL" SUBCIRCUIT
* CREATED USING PARTS RELEASE 4.01 ON 06/16/89 AT 13:08
* (REV N/A)      SUPPLY VOLTAGE: +/-15V
* CONNECTIONS:   NON-INVERTING INPUT
*                | INVERTING INPUT
*                | | POSITIVE POWER SUPPLY
*                | | | NEGATIVE POWER SUPPLY
*                | | | | OUTPUT
*                | | | | |
.SUBCKT TL082    1 2 3 4 5
*
  C1   11 12 3.498E-12
  C2    6  7 15.00E-12
  DC    5 53 DX
  DE   54  5 DX
  DLP  90 91 DX
  DLN  92 90 DX
  DP    4  3 DX
  EGND 99  0 POLY(2) (3,0) (4,0) 0 .5 .5
  FB    7 99 POLY(5) VB VC VE VLP VLN 0 4.715E6 -5E6 5E6 5E6 -5E6
  GA    6  0 11 12 282.8E-6
  GCM   0  6 10 99 8.942E-9
  ISS   3 10 DC 195.0E-6
  HLIM 90  0 VLIM 1K
  J1   11  2 10 JX
  J2   12  1 10 JX
  R2    6  9 100.0E3
  RD1   4 11 3.536E3
  RD2   4 12 3.536E3
  RO1   8  5 150
  RO2   7 99 150
  RP    3  4 2.143E3
  RSS  10 99 1.026E6
  VB    9  0 DC 0
  VC    3 53 DC 2.200
  VE   54  4 DC 2.200
  VLIM  7  8 DC 0
  VLP  91  0 DC 25
  VLN   0 92 DC 25
.MODEL DX D(IS=800.0E-18)
.MODEL JX PJF(IS=15.00E-12 BETA=270.1E-6 VTO=-1)
.ENDS

*
******************************************
*
*1N4148
*
*NXP Semiconductors
*
*High-speed diodes
*
*
*VRRM = 100V
*IFRM = 450 mA 
*trr  = 4ns
*
*Package: SOD 27
*
*Package Pin 1 : Cathode
*Package Pin 2 : Anode
*
*
*
*
*Simulator: PSPICE
*
******************************************
*
.SUBCKT 1N4148 1 2 
*
* The resistor R1 does not reflect 
* a physical device. Instead it
* improves modeling in the reverse 
* mode of operation.
*
R1 1 2 5.827E+9 
D1 1 2 1N4148
*
.MODEL 1N4148 D 
+ IS = 4.352E-9 
+ N = 1.906 
+ BV = 110 
+ IBV = 0.0001 
+ RS = 0.6458 
+ CJO = 7.048E-13 
+ VJ = 0.869 
+ M = 0.03 
+ FC = 0.5 
+ TT = 3.48E-9 
.ENDS
*

.end
